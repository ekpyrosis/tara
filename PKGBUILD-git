# Maintainer: Sinan Alioglu <ekpyrosis@gmail.com>
pkgbase=python-tara-git
pkgname=('python-tara-git' 'python2-tara-git')
_pyname=tara
_gitname=tara
pkgver=20170101
pkgrel=1
pkgdesc='Tara Python Wrapper'
arch=('any')
url='https://github.com/ekpyrosis/tara'
license=('BSD')
makedepends=('git' 'python' 'python2' 'python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("git+https://github.com/ekpyrosis/${_gitname}")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/${_gitname}"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-py2"
}

package_python-tara-git() {
  depends=('python' 'python-setuptools')
  provides=('python-tara')
  conflicts=('python-tara')
  cd "${srcdir}/${_gitname}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}

package_python2-tara-git() {
  depends=('python2' 'python2-setuptools')
  provides=('python2-tara')
  conflicts=('python2-tara')
  cd "${srcdir}/${_gitname}-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

# vim:set ts=2 sw=2 et:
